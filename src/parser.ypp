%{
#include <bits/stdc++.h>
#include "ast.h"
	using namespace std;
 extern "C" int yylex();
  extern int yyparse();
  extern FILE *yyin;
  extern int line_num;
  extern int errors;
  class prog* start = NULL;
   void yyerror(const char *s);
%}

%union
{
	int number;
	char* stringType;
	class prog *start;
	class fields_c *fields;
	class field_c *field;
	class vars_c *vars;
	class var_c *var;
	class meth_c *meth;
	class meths_c *meths;
	class args_c *ars;
	class arg_c *ar;
	class block_c *bl;
	class vars_d_c *vsd;
	class var_d_c *vd;
	class var_names_c *vr_names;
	class statements_c *stats;
	class statement_c *stat;
	class expr_c *exp;
	class location_c *loc;
	class pars_c *pas;
	class function_call_c *fc;
	class literal_c *lt;
	class callout_args_c *cargs;
	class callout_arg_c *carg;
	class assignment_c *assn;
}
%start prom

%token CALLOUT
%token <stringType> ID STRING
%token <number> INT_LITERAL
%token <stringType> CHAR_LITERAL
%token <stringType> BOOL_LITERAL
%token <stringType> EQQ NQQ EQ
%token <stringType> AND OR NOT
%token <stringType> LT GT LE GE
%token <stringType> ADD SUB
%token <stringType> MUL DIV MOD
%token OL CL OS CS CB OB
%token <stringType> TYPE VOID
%token CLASS PROGRAM SC COMMA
%token IF FOR BREAK CONTINUE ELSE RETURN
%token <stringType> PLUSONE SUBONE

%type <start> prom
%type <fields> field_declarations
%type <field> field_declaration
%type <vars> variables;
%type <var> variable;
%type <meth> method_declaration;
%type <meths> method_declarations;
%type <ars> args;
%type <ar> arg;
%type <bl> block;
%type <vsd> var_declarations;
%type <vd> var_declaration;
%type <vr_names> var_names;
%type <stats> statements;
%type <stat> statement;
%type <exp> expr;
%type <loc> location;
%type <pas> pars;
%type <fc> function_call;
%type <lt> literal;
%type <cargs> callout_args;
%type <carg> callout_arg;
%type <assn> assignment;

%left EQQ NQQ
%left AND OR
%left LT GT LE GE
%left ADD SUB
%left MUL DIV MOD
%left NOT


%%

prom : CLASS PROGRAM CB field_declarations method_declarations OB{$$ = new prog($4,$5);start =$$;}
	 | CLASS PROGRAM CB OB {$$ = new prog(); start = $$;}
	 | CLASS PROGRAM CB method_declarations OB{ $$ = new prog(NULL,$4);start = $$;}
	 | CLASS PROGRAM CB field_declarations  OB { $$ = new prog($4,NULL); start = $$;};


field_declarations : field_declaration SC { $$ = new fields_c(); $$->push_back($1);}
					| field_declarations field_declaration SC { $1->push_back($2); $$=$1;};


field_declaration  : TYPE variables { $$ = new field_c($1,$2);};


variables 		   : variable { $$ = new vars_c();$$->push_back($1);}
					| variables COMMA variable { $1->push_back($3); $$ = $1;};


variable           : ID { $$ = new var_c($1);}
					| ID CS INT_LITERAL OS { $$ = new var_c(string($1),$3);};

method_declarations : method_declaration { $$ = new meths_c(); $$->push_back($1);}
					| method_declarations method_declaration { $1->push_back($2); $$ =$1;}

method_declaration  : TYPE ID args block {$$ = new meth_c($1,$2,$3,$4);}
					| VOID ID args block {$$ = new meth_c($1,$2,$3,$4);}

args               : CL OL { $$ =  new args_c();}
					| CL TYPE ID OL { $$  =  new args_c($2,$3);}
					| CL TYPE ID arg OL { $$ = new args_c($2,$3); $$->push_back($4);};

arg                : COMMA TYPE ID { $$ = new arg_c($2,$3); }
					| COMMA TYPE ID arg { $4->push_back($2,$3); $$ = $4;};

block              : CB var_declarations statements OB {$$ = new block_c($2,$3);}
				   | CB statements OB {$$ = new block_c(NULL,$2);}
				   | CB var_declarations OB { $$ = new block_c($2,NULL);}
				   | CB OB { $$ = new block_c();}

var_declarations   : var_declaration SC { $$ = new vars_d_c($1);}
					| var_declaration SC var_declarations { $3->push_back($1); $$=$3;} ;

var_declaration    : TYPE ID { $$ = new var_d_c($1,$2);}
					| TYPE ID var_names { $$ = new var_d_c($1,$2); $$->push_back($3);};

var_names          : COMMA ID { $$ = new var_names_c(); $$->push_back($2);}
					| COMMA ID var_names { $3->push_back($2); $$ = $3;};

statements         : statement { $$ = new statements_c(); $$->push_back($1);}
					| statements statement {$1->push_back($2);$$=$1; };

statement: assignment { $$ = new statement_c($1); }
		 | function_call SC { $$ = new statement_c($1);}
		 | IF CL expr OL block { cout<<"if started"<<endl;$$ = new statement_c($3,$5,NULL);cout<<"if ended\n";}
		 | IF CL expr OL block ELSE block { $$ = new statement_c($3,$5,$7);}
		 | FOR ID EQ expr COMMA expr block {$$ = new statement_c($2,$4,$6,$7);}
		 | RETURN SC {cout<<"RETURN SC\n";$$ = new statement_c("RETURN");}
		 | RETURN expr SC {cout<<"RETURN EXPR SC\n"; $$ = new statement_c("RETURN",$2);}
		 | BREAK SC {$$ = new statement_c("BREAK");}
		 | CONTINUE SC {$$ = new statement_c("CONTINUE");}
		 | block {$$ = new statement_c($1);};

assignment: location EQ expr SC { $$ = new assignment_c($1,$2,$3);}
		  | location PLUSONE expr SC { $$ = new assignment_c($1,$2,$3);}
	      | location SUBONE expr SC { $$ = new assignment_c($1,$2,$3);};


function_call: ID CL pars OL { $$ = new function_call_c($1,$3);}
			 | CALLOUT CL STRING callout_args OL{$$ = new function_call_c("CALLOUT",$3,$4);};

pars   : {$$ = new pars_c();}
			 | expr {$$ = new pars_c(); $$->push_back($1);}
			 | pars COMMA expr {$1->push_back($3); $$ = $1;};


location : ID {$$ = new location_c($1);}
		 | ID CS expr OS {$$ = new location_c($1,$3);};

callout_args: {$$ = new callout_args_c();}
           | callout_args COMMA callout_arg { $1->push_back($3); $$ = $1;};

callout_arg: expr {$$ = new callout_arg_c($1);}
		   | STRING {$$ = new callout_arg_c($1);};

expr : CL expr OL {$$ = new expr_c($2);}
	 |  location {$$ = new expr_c($1);}
	 |  function_call {$$ = new expr_c($1);}
	 |  literal {$$ = new expr_c($1);}
	 |  expr SUB expr {$$ = new expr_c($1,$2,$3);}
	 |  expr OR expr {$$ = new expr_c($1,$2,$3);}
	 |  expr AND expr {$$ = new expr_c($1,$2,$3);}
	 |  expr NQQ expr {$$ = new expr_c($1,$2,$3);}
	 |  expr EQQ expr {$$ = new expr_c($1,$2,$3);}
	 |  expr GE expr {$$ = new expr_c($1,$2,$3);}
	 |  expr LE expr {$$ = new expr_c($1,$2,$3);}
	 |  expr GT expr {$$ = new expr_c($1,$2,$3);}
	 |  expr LT expr {$$ = new expr_c($1,$2,$3);}
	 |  expr MOD expr {$$ = new expr_c($1,$2,$3);}
	 |  NOT expr  {$$ = new expr_c($1,$2);}
	 |  expr DIV expr {$$ = new expr_c($1,$2,$3);}
	 |  expr MUL expr {$$ = new expr_c($1,$2,$3);}
	 |  expr ADD expr {$$ = new expr_c($1,$2,$3);}
	 |  SUB expr {$$ = new expr_c($1,$2);}
	 ;

literal : INT_LITERAL {  $$ = new literal_c($1);}
		| CHAR_LITERAL { $$ = new literal_c($1);}
		| BOOL_LITERAL { $$ = new literal_c($1);};



%%

main(int argc, char **argv)
{
	if(argc == 1)
    {
		fprintf(stderr, "Store the input expressions in a file name and give it as a argument\n");
		exit(1);
	}
	//printf("Parsing started\n");
	yyin = fopen(argv[1], "r");
	yyparse();
	cout<<"Parsing done"<<endl;
	dfs* df = new dfs();
	//start->accept(df);
	if(start)
	{
		start->generateCode();
		start->generateCodeDump();
	}
	//printf("THE END\n");
}

void yyerror(const char *s)
{
	fprintf(stderr, "error: %s\n", s);
}
